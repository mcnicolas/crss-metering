buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://repo.spring.io/plugins-release'
        }

        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE'
        classpath 'io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE'
        classpath 'org.ajoberstar:gradle-git:1.5.1'
        classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:1.6.2'
    }
}

ext {
    springBootVersion = '1.4.2.RELEASE'
    git = org.ajoberstar.grgit.Grgit.open()
    revision = git.head().abbreviatedId
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    project.buildDir = 'target'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://repo.spring.io/plugins-release'
        }

        // this two repository must not be here since it was already
        jcenter()
        mavenCentral()
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    configurations {
        compile.exclude module: 'spring-boot-starter-tomcat'
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR3'
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
        }

        dependencies {
            dependency ("org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion") {
                exclude 'org.apache.tomcat:tomcat-jdbc'
            }

            dependencySet(group:'org.apache.poi', version:'3.15') {
                entry 'poi'
                entry 'poi-ooxml'
            }

            dependency 'net.sf.supercsv:super-csv-java8:2.4.0'

            dependency 'commons-io:commons-io:2.5'
            dependency 'org.apache.commons:commons-lang3:3.5'

            dependencySet(group:'org.apache.httpcomponents', version:'4.5.2') {
                entry 'httpclient'
                entry 'httpmime'
            }

            dependency 'org.json:json:20160810'

            dependency 'org.projectlombok:lombok:1.16.10'

            // Logging
            dependencySet(group:'org.slf4j', version:'1.7.21') {
                entry 'slf4j-api'
                entry 'jcl-over-slf4j'
            }

            dependencySet(group:'ch.qos.logback', version:'1.1.7') {
                entry 'logback-core'
                entry 'logback-classic'
            }

            dependency 'de.appelgriepsch.logback:logback-gelf-appender:1.2'

            // DB Related
            dependency 'com.zaxxer:HikariCP:2.5.1'
            dependency 'org.postgresql:postgresql:9.4.1211'

            dependency 'org.springframework.security.oauth:spring-security-oauth2:2.0.10.RELEASE'
        }
    }

}

configure(subprojects - project(':commons-web') - project(':commons-service') - project(':commons-log')) {
    apply plugin: 'org.springframework.boot'
}
