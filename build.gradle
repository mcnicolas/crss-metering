apply from: file("$rootDir/gradle/sonar.gradle")

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://repo.spring.io/plugins-release'
        }

        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE'
        classpath 'io.spring.gradle:dependency-management-plugin:0.6.1.RELEASE'
        classpath 'com.github.ksoichiro:gradle-build-info-plugin:0.2.0'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.1'
    }
}

ext {
    springBootVersion = '1.5.2.RELEASE'
    applicationDefaultJvmArgs = ['-Xms128m', '-Xmx256m']
    projectVersion = project.version
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.github.ksoichiro.build.info'

    project.buildDir = 'target'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        // this two repository must not be here since it was already
        jcenter()
        mavenCentral()
        mavenLocal()
    }

    idea {
        module {
            downloadJavadoc = false
            downloadSources = true
        }
    }

    configurations {
        compile.exclude module: 'spring-boot-starter-tomcat'
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Camden.SR6'
            mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
        }

        dependencies {
            dependencySet(group:'io.undertow', version:'1.4.9.Final') {
                entry 'undertow-core'
                entry 'undertow-servlet'
                entry 'undertow-websockets-jsr'
            }

            dependency ("org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion") {
                exclude 'org.apache.tomcat:tomcat-jdbc'
            }

            dependencySet(group:'org.apache.poi', version:'3.15') {
                entry 'poi'
                entry 'poi-ooxml'
            }

            dependency 'net.sf.supercsv:super-csv-java8:2.4.0'
            dependency 'commons-io:commons-io:2.5'
            dependency 'commons-beanutils:commons-beanutils:1.9.3'
            dependency 'org.apache.commons:commons-lang3:3.5'
            dependency 'com.google.guava:guava:19.0'

            dependency 'net.sf.ehcache:ehcache:2.10.3'

            dependencySet(group:'org.apache.httpcomponents', version:'4.5.3') {
                entry 'httpclient'
                entry 'httpmime'
            }

            dependency 'org.json:json:20160810'

            dependency 'org.projectlombok:lombok:1.16.12'

            // Logging
            dependencySet(group:'org.slf4j', version:'1.7.22') {
                entry 'slf4j-api'
                entry 'jcl-over-slf4j'
            }

            dependencySet(group:'ch.qos.logback', version:'1.1.8') {
                entry 'logback-core'
                entry 'logback-classic'
            }

            dependency 'de.appelgriepsch.logback:logback-gelf-appender:1.2'

            // DB Related
            dependency 'com.zaxxer:HikariCP:2.6.0'
            dependency 'org.postgresql:postgresql:9.4.1212'

            dependency 'org.springframework.security.oauth:spring-security-oauth2:2.0.12.RELEASE'

            dependency 'junit:junit:4.12'

            dependencySet(group: 'org.spockframework', version: '1.1-groovy-2.4-rc-2') {
                entry 'spock-core'
                entry 'spock-spring'
            }

            dependency "com.pemc.crss:crss-shared-commons-util:$projectVersion"

        }
    }

}
